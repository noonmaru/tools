plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.9.0'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

group 'com.github.noonmaru.tools'
version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'org.ow2.asm', name: 'asm', version: '7.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

publishing {
    publications {
        LibPublication(MavenPublication) {
            artifactId = 'tools'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Noonmaru'
                description = 'Noonmaru tools'
                url = 'https://github.com/noonmaru/tools'
                licenses {
                    license {
                        name = 'GPL-2.0'
                        url = 'https://opensource.org/licenses/gpl-2.0.php'
                    }
                }
                developers {
                    developer {
                        id = 'noonmaru'
                        name = 'Noonmaru'
                        email = 'noonmaru10@gmail.com'
                        url = 'https://github.com/noonmaru'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/noonmaru/tools.git'
                    developerConnection = 'scm:git:ssh://github.com/noonmaru/tools.git'
                    url = 'https://github.com/noonmaru/tools'
                }
            }
        }
    }
}